defaultTasks 'clean', 'build', 'templateProjectZip'

final groovyVersion    = '2.0.4'

allprojects {
    apply plugin: 'idea'
    version = '1.3-SNAPSHOT'
    group = 'org.gaelyk'
}

subprojects {
    ext.appEngineVersion = '1.7.1'

    apply plugin: 'groovy'

    repositories {
        mavenCentral()
    }

    dependencies {
        groovy "org.codehaus.groovy:groovy-all:$groovyVersion"
    }
}

ext.templateProjectDir = 'template-project'
ext.gradleWrapperDir = 'gradle'

task templateProjectZip(type: Zip) {
    description = 'Creates template project distribution.'

    from("$templateProjectDir/src") {
        into 'src'
    }

    from(gradleWrapperDir) {
        into 'gradle'
    }

    from(templateProjectDir) {
        include 'build.gradle'
    }
    
    from('.') {
        include 'gradlew', 'gradlew.bat'
        fileMode = 0755
    }

    baseName 'gaelyk-template-project'
}

task wrapper(type: Wrapper) {
    gradleVersion '1.2'
}
